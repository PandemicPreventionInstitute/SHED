"""
Writen by Devon Gregory
This snakefile is meant to be run via snakemake to finish the
bioinformatics processing for SRA samples.
Last edited on 11-23-22
"""
import os
#from snakemake.utils import min_version

#min_version("7.8.0")


snakepath = os.path.realpath(sys.path[0])
configfile: f"{snakepath}/config.yaml"


include: f"{snakepath}/modules/snakefunctions.py"

# dertermine working dir path
if not config["work_path"]:
    config["work_path"] = os.getcwd()
workdir: config["work_path"]

UPDATE = config["freyja_update"]
sra_accs = get_sample_acc2(config["reprocess"])

rule all:
    """
    Establishes targets for snakemake and initial wildcard values
    """
    input:
        expand("sams/{sra_acc}.trim.done", sra_acc=sra_accs),
        "endpoints/aggregate.done",
        expand("{sra_acc}.cleanup2.done", sra_acc=sra_accs),

rule primer_trim:
    """
    trim primers from samples where the sequencing primers can be determined using ivar
    """
    input:
        "sams/{sra_acc}.bam",
    output:
        temp(touch("sams/{sra_acc}.trim.done")),
    params:
        bed=lambda wildcards: (sra_accs[wildcards.sra_acc]["bed"]),
    log:
        "logs/{sra_acc}.trim.log",
    conda:
        "envs/freyja.yaml"
    shell:
        """
        if [ ! {params.bed} == 'Unknown' ]
        then
        ivar trim -b {snakepath}/{params.bed} -p sams/{wildcards.sra_acc}.trimmed \
        -i {input} -e -q 15 -m 30 -s 4  >>{log} 2>&1 && \
        samtools sort sams/{wildcards.sra_acc}.trimmed.bam -o sams/{wildcards.sra_acc}.trimmed.sorted.bam >>{log} 2>&1 \
        && samtools index sams/{wildcards.sra_acc}.trimmed.sorted.bam >>{log} 2>&1
        fi
        """


rule consensus:
    """
    uses ivar to generate a consensus sequence
    uses primer trimmed bam if available, otherwise uses basal bam
    """
    input:
        "sams/{sra_acc}.trim.done",
        fa=f"{snakepath}/data/SARS2.fasta",
    output:
        "endpoints/{sra_acc}.fa",
    log:
        "logs/{sra_acc}.con.log",
    conda:
        "envs/freyja.yaml"
    shell:
        """
        if [[ -f sams/{wildcards.sra_acc}.trimmed.sorted.bam ]]
        then
        samtools mpileup -aa -A -d 600000 -B -Q 0 sams/{wildcards.sra_acc}.trimmed.sorted.bam 2>>{log} | \
        ivar consensus -p endpoints/{wildcards.sra_acc} -q 15 -t 0.5 >>{log} 2>&1
        else
        samtools mpileup -aa -A -d 600000 -B -Q 0 sams/{wildcards.sra_acc}.bam 2>>{log} | \
        ivar consensus -p endpoints/{wildcards.sra_acc} -q 15 -t 0.5 >>{log} 2>&1
        fi
        """


rule vc:
    """
    calls variants with ivar and depths with samtools
    trimmed bams are used if primers were discovered, otherwise basal bams are used
    """
    input:
        "sams/{sra_acc}.trim.done",
        fa=f"{snakepath}/data/SARS2.fasta",
        gff=f"{snakepath}/data/NC_045512.2.gff3",
    output:
        tsv="endpoints/{sra_acc}.tsv",
        depth="endpoints/{sra_acc}.depth",
    log:
        "logs/{sra_acc}.ivar.log",
    conda:
        "envs/freyja.yaml"
    shell:
        """
        if [[ -f sams/{wildcards.sra_acc}.trimmed.sorted.bam ]]
        then
        samtools mpileup -aa -A -d 600000 -B -Q 0 \
        sams/{wildcards.sra_acc}.trimmed.sorted.bam | ivar variants -p \
        endpoints/{wildcards.sra_acc} -q 0 -t 0 -r {input.fa} -g {input.gff} > {log} 2>&1
        samtools mpileup -aa -A -d 600000 -Q 0 -q 0 -B -f {input.fa} \
        sams/{wildcards.sra_acc}.trimmed.sorted.bam | cut -f1-4 > {output.depth} 2> {log}
        else
        samtools mpileup -aa -A -d 600000 -B -Q 0 \
        sams/{wildcards.sra_acc}.bam | ivar variants -p \
        endpoints/{wildcards.sra_acc} -q 0 -t 0 -r {input.fa} -g {input.gff} > {log} 2>&1
        samtools mpileup -aa -A -d 600000 -Q 20 -q 0 -B -f {input.fa} \
        sams/{wildcards.sra_acc}.bam | cut -f1-4 > {output.depth} 2> {log}
        fi
        """


rule update_freyja:
    """
    makes sure freyja is up to date
    """
    output:
        temp(touch("freyja.updated")),
    conda:
        "envs/freyja.yaml"
    shell:
        """
        if [[ {UPDATE} == True ]]
        then
        freyja update
        echo $(date +'%Y-%m-%d.%T') >> freyja.update.times
        fi
        """


rule lineages:
    """
    generates a lineage report using freyja
    """
    input:
        rules.update_freyja.output,
        infiles=rules.vc.output,
    output:
        "endpoints/{sra_acc}.lineages.tsv",
    log:
        "logs/{sra_acc}.lin.log",
    conda:
        "envs/freyja.yaml"
    threads: 10
    shell:
        "freyja demix {input.infiles} --output {output} >>{log} 2>&1"


rule aggregate:
    """
    calls aggregation function to collect sample data into aggregate files
    potential TODO: add aggregation of metadata
    """
    input:
        vcs=expand(
            "endpoints/{sra_acc}.tsv", sra_acc=sra_accs
            ),
        cons=expand(
            "endpoints/{sra_acc}.fa", sra_acc=sra_accs
            ),
        lins=expand(
            "endpoints/{sra_acc}.lineages.tsv",
            sra_acc=sra_accs,
            ),
    output:
        temp(touch("endpoints/aggregate.done")),
    run:
        aggregate_endpoints(input.vcs, input.cons, input.lins, config["reprocess"])


rule cleanup:
    """
    removes unwanted intermediate files
    """
    input:
        rules.aggregate.output,
        "endpoints/{sra_acc}.tsv",
        "endpoints/{sra_acc}.fa",
        "endpoints/{sra_acc}.lineages.tsv",
    output:
        temp(touch("{sra_acc}.cleanup2.done")),
    run:
        if config["cleanup"]:
            shell("rm sams/{wildcards.sra_acc}*trimmed.bam")
            if os.path.isfile(f"sams/{wildcards.sra_acc}.trimmed.sorted.bam"):
                shell("rm sams/{wildcards.sra_acc}.bam")
                shell("rm sams/{wildcards.sra_acc}.bam.bai")
